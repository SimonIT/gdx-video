configure([
        project(":gdx-video-core"),
        project(":gdx-video-android"),
        project(":gdx-video-desktop"),
        project(":gdx-video-desktop:gdx-video-lwjgl"),
        project(":gdx-video-desktop:gdx-video-lwjgl3"),
        project(":gdx-video-gwt"),
        project(":gdx-video-robovm")
])
        {
            apply plugin: 'maven-publish'
            apply plugin: 'signing'

            afterEvaluate { project ->
                //Workaround android not having components populated yet.
                afterEvaluate {
                    publishing {
                        publications {
                            mavenJava(MavenPublication) {
                                artifactId = archivesBaseName ?: project.name
                                
                                //Most normal java projects
                                if (components.findByName("java") != null)
                                    from components.java

                                //Android
                                if (components.findByName("release") != null) {
                                    from components.release
                                }

                                pom {
                                    name.set(POM_NAME)
                                    description.set(POM_DESCRIPTION)
                                    url.set(POM_URL)

                                    scm {
                                        url.set(POM_SCM_URL)
                                        connection.set(POM_SCM_CONNECTION)
                                        developerConnection.set(POM_SCM_DEV_CONNECTION)
                                    }

                                    licenses {
                                        license {
                                            name.set(POM_LICENCE_NAME)
                                            url.set(POM_LICENCE_URL)
                                            distribution.set(POM_LICENCE_DIST)
                                        }
                                    }
                                    developers {
                                        developer {
                                            id.set('SimonIT')
                                            name.set('Simon Bullik')
                                            email.set('simonit.orig@gmail.com')
                                        }
                                    }
                                }
                            }
                        }

                        repositories {
                            maven {
                                url = version.endsWith('SNAPSHOT') ? getSnapshotRepositoryUrl() : getReleaseRepositoryUrl()

                                if (getRepositoryUsername() || getRepositoryPassword()) {
                                    credentials {
                                        username = getRepositoryUsername()
                                        password = getRepositoryPassword()
                                    }
                                }
                            }
                        }
                    }

                    signing {
                        useGpgCmd()
                        sign publishing.publications.mavenJava
                    }

                    //Simply using "required" in signing block doesn't work because taskGraph isn't ready yet.
                    gradle.taskGraph.whenReady {
                        tasks.withType(Sign).tap {
                            configureEach {
                                onlyIf { isReleaseBuild() }
                            }
                        }
                    }
                }
            }
        }
